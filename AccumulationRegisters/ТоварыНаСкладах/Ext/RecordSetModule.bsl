
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если НЕ ВыполнятьПроверку() Тогда
		Возврат;
	КонецЕсли;
	
	БлокироватьДляИзменения = Истина;
	
	МенеджерВТ = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Таблица.Номенклатура КАК Номенклатура,
	|	Таблица.Склад КАК Склад,
	|	ВЫБОР
	|		КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА Таблица.Количество
	|		ИНАЧЕ -Таблица.Количество
	|	КОНЕЦ КАК Количество
	|ПОМЕСТИТЬ ДвиженияТоварыНаСкладахПередЗаписью
	|ИЗ
	|	РегистрНакопления.ТоварыНаСкладах КАК Таблица
	|ГДЕ
	|	Таблица.Регистратор = &Регистратор";
	
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	РезультатЗапроса = Запрос.Выполнить();
	
	ДополнительныеСвойства.Вставить("МенеджерВТ", МенеджерВТ);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если НЕ ВыполнятьПроверку() Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВТ;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаИзменений.Номенклатура КАК Номенклатура,
	|	ТаблицаИзменений.Склад КАК Склад
	|ПОМЕСТИТЬ ДвиженияТоварыНаСкладахИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		Таблица.Номенклатура КАК Номенклатура,
	|		Таблица.Склад КАК Склад,
	|		-Таблица.Количество КАК Количество
	|	ИЗ
	|		ДвиженияТоварыНаСкладахПередЗаписью КАК Таблица
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Таблица.Номенклатура,
	|		Таблица.Склад,
	|		ВЫБОР
	|			КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА Таблица.Количество
	|			ИНАЧЕ -Таблица.Количество
	|		КОНЕЦ
	|	ИЗ
	|		РегистрНакопления.ТоварыНаСкладах КАК Таблица
	|	ГДЕ
	|		Таблица.Регистратор = &Регистратор) КАК ТаблицаИзменений
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаИзменений.Номенклатура,
	|	ТаблицаИзменений.Склад
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаИзменений.Количество) <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПРЕДСТАВЛЕНИЕ(Остатки.Номенклатура) КАК НоменклатураПредставление,
	|	ПРЕДСТАВЛЕНИЕ(Остатки.Склад) КАК СкладПредставление,
	|	-Остатки.КоличествоОстаток КАК Количество
	|ИЗ
	|	РегистрНакопления.ТоварыНаСкладах.Остатки(
	|			,
	|			(Номенклатура, Склад) В
	|				(ВЫБРАТЬ
	|					ТаблицаИзменений.Номенклатура КАК Номенклатура,
	|					ТаблицаИзменений.Склад КАК Склад
	|				ИЗ
	|					ДвиженияТоварыНаСкладахИзменение КАК ТаблицаИзменений)) КАК Остатки
	|ГДЕ
	|	Остатки.КоличествоОстаток < 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ДвиженияТоварыНаСкладахПередЗаписью";
	
	Регистратор = Отбор.Регистратор.Значение;
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ТекстОшибки = СтрШаблон("На складе %1 недостаточно номенклатуры %2 в количестве %3",
			Выборка.СкладПредставление,
			Выборка.НоменклатураПредставление,
			Выборка.Количество);
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, Регистратор,,, Отказ);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли

#Область СлужебныеПроцедурыИФункции

Функция ВыполнятьПроверку()
	Возврат НЕ ОбменДанными.Загрузка;
КонецФункции

#КонецОбласти

