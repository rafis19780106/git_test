
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПриЧтенииСозданииНаСервере();

КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПриЧтенииСозданииНаСервере();
	КонецЕсли;
	
	УстановитьУсловноеОформление();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииСозданииНаСервере()
	
	ЗаполнитьДанныеФормы();
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ЗаполнитьДанныеФормы();
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	НепроверяемыеРеквизиты = Новый Массив;
	
	Если НеВыполнятьПроверкуОбъекта Тогда
		НепроверяемыеРеквизиты.Добавить("Объект");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, НепроверяемыеРеквизиты);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыНоменклатураПриИзменении(Элемент)
	
	СтрокаТоваров = Элементы.Товары.ТекущиеДанные;
	
	НоменклатураДокумента = Новый Массив;
	НоменклатураДокумента.Добавить(СтрокаТоваров.Номенклатура);
	
	ТипыНоменклатуры = ПолучитьТипыНоменклатуры(НоменклатураДокумента);
	СтрокаТоваров.ТипНоменклатуры = ТипыНоменклатуры[СтрокаТоваров.Номенклатура];
	
	Если СтрокаТоваров.ТипНоменклатуры = ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Услуга") Тогда
		СтрокаТоваров.Склад = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСкладПриИзменении(Элемент)
	СтрокаТоваров = Элементы.Товары.ТекущиеДанные;
	СтрокаТоваров.Подразделение = ПодразделениеСклада(СтрокаТоваров.Склад);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьДанныеФормы()
	
	НоменклатураДокумента = ПолучитьНоменклатуруДокумента();
	ТипыНоменклатуры = ПолучитьТипыНоменклатуры(НоменклатураДокумента);
	
	Для Каждого СтрокаТоваров Из Объект.Товары Цикл
		СтрокаТоваров.ТипНоменклатуры = ТипыНоменклатуры[СтрокаТоваров.Номенклатура];
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	ОбщегоНазначенияКлиентСервер.ДобавитьПростойЭлементУсловногоОформления(ЭтаФорма,
		"ТоварыСклад", "Видимость", Ложь, 
		"Объект.Товары.ТипНоменклатуры", ВидСравненияКомпоновкиДанных.Равно, Перечисления.ТипыНоменклатуры.Услуга);
	
	// На неравенство, чтобы не показывать при пустой номенклатуре
	ОбщегоНазначенияКлиентСервер.ДобавитьПростойЭлементУсловногоОформления(ЭтаФорма,
		"ТоварыПодразделение", "Видимость", Ложь, 
		"Объект.Товары.ТипНоменклатуры", ВидСравненияКомпоновкиДанных.НеРавно, Перечисления.ТипыНоменклатуры.Услуга);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьНоменклатуруДокумента()
	
	ВозвращаемоеЗначение = Новый Массив;
	
	Для Каждого СтрокаТоваров Из Объект.Товары Цикл
		ВозвращаемоеЗначение.Добавить(СтрокаТоваров.Номенклатура);
	КонецЦикла;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции 

&НаСервереБезКонтекста
Функция ПолучитьТипыНоменклатуры(МассивНоменклатуры)
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъектов(МассивНоменклатуры, "Тип");
КонецФункции

&НаСервереБезКонтекста
Функция ПодразделениеСклада(Склад)
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Склад, "Подразделение");
КонецФункции

#КонецОбласти

